buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.9.0'
    }
}

repositories {
    mavenCentral()
    maven {
        url "http://oss.sonatype.org/content/repositories/snapshots"
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'org.junit.platform.gradle.plugin'

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

version = '@MONETDBLITEJAVA_NEXT_MAJOR_VERSION@.@MONETDBLITEJAVA_NEXT_MINOR_VERSION@@MONETDBLITEJAVA_NEXT_SNAPSHOT@'
ext.version = version
group = 'monetdb'
ext.group = group
archivesBaseName = 'monetdb-java-lite'
ext.archivesBaseName = archivesBaseName
ext.packaging = 'jar'

dependencies {
    //New MonetDB JDBC driver
    compile("${group}:monetdb-jdbc-new:@NEW_JDBC_NEXT_MAJOR_VERSION@.@NEW_JDBC_NEXT_MINOR_VERSION@@NEW_JDBC_NEXT_SNAPSHOT@")
    runtime("${group}:monetdb-jdbc-new:@NEW_JDBC_NEXT_MAJOR_VERSION@.@NEW_JDBC_NEXT_MINOR_VERSION@@NEW_JDBC_NEXT_SNAPSHOT@")

    //JUnit Jupiter API and TestEngine implementation
    testCompile('org.junit.jupiter:junit-jupiter-api:5.5.0-M1')
    testCompile('org.junit.jupiter:junit-jupiter-engine:5.5.0-M1')

    //Log using log4j
    testCompile('org.apache.logging.log4j:log4j-core:2.11.2')
    testCompile('org.apache.logging.log4j:log4j-jul:2.11.2')
}

junitPlatform { //configurations for JUnit5 tests
    platformVersion '1.0.0'
    logManager 'org.apache.logging.log4j.jul.LogManager'
    reportsDir file('build/test-results/junit-platform')
}

jar {
    manifest {
        attributes 'Description': 'Serveless version of the MonetDB database in the JVM - JDBC compliant'
    }
}

wrapper { //Set the gradle wrapper, so users don't have to install Gradle
    gradleVersion = '6.6'
}

task javadocJar(type: Jar) { //Generate the javadocs jar
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) { //Generate the sources jar
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives { //do gradle uploadArchives
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'monetdb-java-lite'
                artifactId 'monetdb-java-lite'
                packaging 'jar'
                description 'Serveless version of the MonetDB database in the JVM - JDBC compliant'
                url 'https://github.com/hannesmuehleisen/MonetDBLite-Java/tree/master'

                scm {
                    url 'https://github.com/hannesmuehleisen/MonetDBLite-Java/tree/master'
                    connection 'scm:git:git://github.com/hannesmuehleisen/MonetDBLite-Java.git'
                    developerConnection 'scm:git:git@github.com:hannesmuehleisen/MonetDBLite-Java.git'
                }

                licenses {
                    license {
                        name 'Mozilla Public License, Version 2.0'
                        url 'https://www.mozilla.org/MPL/2.0/'
                    }
                }

                developers {
                    developer {
                        id 'ferreira'
                        name 'Pedro Ferreira'
                        email 'pedro.ferreira@monetdbsolutions.com'
                    }
                }
            }
        }
    }
}

nexusStaging { //do gradle closeAndReleaseRepository
    packageGroup = "${group}.${archivesBaseName}"
}
